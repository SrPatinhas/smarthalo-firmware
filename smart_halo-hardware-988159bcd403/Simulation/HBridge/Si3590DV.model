*May 16, 2005
*Doc. ID: 76950, S-50836, Rev. B
*Dn Gn Sn Dp Gp Sp
.SUBCKT Si3590DV 6 2 1 3 5 4
X1 6 2 1 Si3590N
X2 3 5 4 Si3590P
.ENDS Si3590DV
*N-Ch
.SUBCKT Si3590N 4 1 2
M1  3 1 2 2 NMOS W=345253u L=0.25u
M2  2 1 2 4 PMOS W=345253u L=0.45u
R1  4 3     RTEMP 38E-3
CGS 1 2     200E-12
DBD 2 4     DBD
***************************************************************
.MODEL  NMOS       NMOS  ( LEVEL  = 3          TOX    = 3E-8
+ RS     = 16E-3           RD     = 0          NSUB   = 2.9E17
+ KP     = 3.1E-5          U0     = 650
+ VMAX   = 0               XJ     = 5E-7       KAPPA  = 1E-1
+ ETA    = 1E-4            TPG    = 1
+ IS     = 0               LD     = 0
+ CGSO   = 0               CGDO   = 0          CGBO   = 0
+ TLEV   = 1               BEX    = -1.5       TCV    = 2.8E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
***************************************************************
.MODEL  PMOS       PMOS  ( LEVEL  = 3          TOX    = 3E-8
+NSUB    = 4E16            TPG    = -1)
 **************************************************************
.MODEL DBD D (CJO=70E-12 VJ=0.38 M=0.25
+RS=0.1 FC=0.1 IS=1E-12 TT=5E-8 N=1 BV=30.2)
***************************************************************
.MODEL RTEMP R (TC1=6E-3 TC2=5.5E-6)
***************************************************************
.ENDS Si3590N
*P-CH
.SUBCKT Si3590P 4 1 2
M1  3 1 2 2 PMOS W=339769u L=0.25u
M2  2 1 2 4 NMOS W=339769u L=0.55u
R1  4 3     RTEMP 18E-3
CGS 1 2     200E-12
DBD 4 2     DBD
***************************************************************
.MODEL  PMOS        PMOS ( LEVEL  = 3          TOX    = 3E-8
+ RS     = 89E-3           RD     = 0          NSUB   = 6.5E16
+ KP     = 1E-5            UO     = 400
+ VMAX   = 0               XJ     = 5E-7       KAPPA  = 4E-2
+ ETA    = 1E-4            TPG    = -1
+ IS     = 0               LD     = 0
+ CGSO   = 0               CGDO   = 0          CGBO   = 0
+ TLEV   = 1               BEX    = -1.5       TCV    = 3E-3
+ NFS    = 0.8E12          DELTA  = 0.1)
***************************************************************
.MODEL  NMOS        NMOS ( LEVEL  = 3          TOX    = 3E-8
+NSUB    = 6E16            TPG    = -1)
***************************************************************
.MODEL DBD D (CJO=60E-12 VJ=0.38 M=0.30
+RS=0.1 FC=0.5 IS=1E-12 TT=6E-8 N=1 BV=30.2)
***************************************************************
.MODEL RTEMP R (TC1=14E-3 TC2=5.5E-6)
***************************************************************
.ENDS Si3590P
